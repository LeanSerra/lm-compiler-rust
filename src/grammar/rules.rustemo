Program: TokenId TokenParOpen TokenParClose TokenCBOpen Body TokenCBClose {ProgramWithMain}
 | Body {ProgramOnlyBody};

Body: TokenInit InitBody Expressions {BodyInitExpressions}
 | TokenInit InitBody {BodyInit}
 | Expressions {BodyExpressions}
 | EMPTY {BodyEmpty};

InitBody: TokenCBOpen VarDeclarations TokenCBClose {InitBody};

FunctionRead: TokenRead TokenParOpen TokenId TokenParClose {FunctionReadCall};

FunctionWrite: TokenWrite TokenParOpen SimpleExpression TokenParClose {FunctionWriteCall};

FunctionIsZero: TokenIsZero TokenParOpen ArithmeticExpression TokenParClose {FunctionIsZeroCall};

FunctionConvDate: TokenConvDate TokenParOpen TokenDate TokenParClose {FunctionConvDateVariableCall};

VarDeclarations: VarDeclaration {VarDeclarationsSingle}
 | VarDeclaration VarDeclarations {VarDeclarationsRecursive};

VarDeclaration: TokenId TokenColon DataType {VarDeclarationSingle}
 | TokenId TokenComma VarDeclaration {VarDeclarationRecursive};

Expressions: Statement {ExpressionSingle}
 | Statement Expressions {ExpressionRecursive};

Statement: Assignment {StatementAssignment}
 | IfStatement {StatementIfStatement}
 | ElseStatement {StatementElseStatement}
 | WhileLoop {StatementWhile}
 | FunctionWrite {StatementWrite}
 | FunctionRead {StatementRead};

Assignment: TokenId TokenAssign SimpleExpression {AssignmentExpression}
 | TokenId TokenAssign FunctionConvDate {AssignmentConvDate};

DataType: TokenInt {IntType}
 | TokenFloat {FloatType}
 | TokenString {StringType};

WhileLoop: TokenWhile TokenParOpen Conjunction TokenParClose TokenCBOpen Body TokenCBClose {While};

IfStatement: TokenIf TokenParOpen Conjunction TokenParClose TokenCBOpen Body TokenCBClose {IfStatement};

ElseStatement: TokenElse TokenCBOpen Body TokenCBClose {ElseStatement};

BooleanExpression: SimpleExpression ComparisonOp SimpleExpression {BooleanExpressionSimpleExpression}
 | TokenTrue {BooleanExpressionTrue}
 | TokenFalse {BooleanExpressionFalse}
 | NotStatement {BooleanExpressionNotStatement}
 | FunctionIsZero {BooleanExpressionIsZero}
 | TokenId {BooleanExpressionVar};

SimpleExpression: ArithmeticExpression {SimpleExpressionArithmetic}
 | TokenStringLiteral {SimpleExpressionString};

Conjunction: BooleanExpression TokenAnd Conjunction{ConjunctionAnd}
 | BooleanExpression TokenOr Conjunction {ConjunctionOr}
 | BooleanExpression {ConjunctionBoolean};

ComparisonOp: TokenEqual {ComparisonOpEqual}
 | TokenNotEqual {ComparisonOpNotEqual}
 | TokenLess {ComparisonOpLess}
 | TokenLessEqual {ComparisonOpLessEqual}
 | TokenGreater {ComparisonOpGreater}
 | TokenGreaterEqual {ComparisonOpGreaterEqual};

Number: TokenIntLiteral {NumberInt}
 | TokenFloatLiteral {NumberFloat}
 | TokenSub TokenIntLiteral {NumberNegativeInt}
 | TokenSub TokenFloatLiteral {NumberNegativeFloat};

NotStatement: TokenNot BooleanExpression {Not};

ArithmeticExpression: ArithmeticExpression DummyAE TokenSum Term {ArithmeticExpressionSumTerm}
| ArithmeticExpression DummyAE TokenSub Term {ArithmeticExpressionSubTerm}
| Term {ArithmeticExpressionTerm};

DummyAE: EMPTY;

Term: Term DummyT TokenMul Factor {TermMulFactor}
| Term DummyT TokenDiv Factor {TermDivFactor}
| Factor {TermFactor};

DummyT: EMPTY;

Factor: TokenId {FactorId}
| Number {FactorNumber}
| TokenParOpen ArithmeticExpression TokenParClose {FactorParen};

terminals

TokenInt:;
TokenFloat:;
TokenString:;
TokenIntLiteral:;
TokenFloatLiteral:;
TokenStringLiteral:;
TokenId:;
TokenAssign:;
TokenSum:;
TokenMul:;
TokenSub:;
TokenDiv:;
TokenParOpen:;
TokenParClose:;
TokenCBOpen:;
TokenCBClose:;
TokenColon:;
TokenInit:;
TokenWhile:;
TokenEqual:;
TokenNotEqual:;
TokenLess:;
TokenLessEqual:;
TokenGreater:;
TokenGreaterEqual:;
TokenTrue:;
TokenFalse:;
TokenIf:;
TokenElse:;
TokenComma:;
TokenAnd:;
TokenOr:;
TokenNot:;
TokenRead:;
TokenWrite:;
TokenIsZero:;
TokenConvDate:;
TokenDate:;
